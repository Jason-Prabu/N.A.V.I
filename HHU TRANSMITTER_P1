#include <Keypad.h>               // FOR KEYPAD
#include <SPI.h>                  // FOR NRF24LO1
#include <nRF24L01.h>
#include <RF24.h>                 //RF LIBRARY

RF24 radio(7, 8);                                         // CE AND CSN PINS CONNECTED TO D7 AND D8
const uint64_t pipes[1] = {0xF0F0F0F0E1LL};               // TRANSMITTER ADDRESS

const int buzzer = 9;               // +VE TERMINAL OF BUZZER CONNECTED TO D9 THROUGHN RESISTOR

char bus_arr[6];
int k = 0, send_flag = 0;

const byte ROWS = 4; //four rows
const byte COLS = 4; //three columns
char keys[ROWS][COLS] = {
  {'1', '2', '3', 'A'},
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};

byte rowPins[ROWS] = {A0, A1, A2, A3}; //connect to the row pinouts of the kpd
byte colPins[COLS] = {2, 3, 4, 5}; //connect to the column pinouts of the kpd

Keypad kpd = Keypad( makeKeymap(keys), rowPins, colPins, ROWS, COLS );


void setup() {

  Serial.begin(9600);

  radio.begin();                          // START RADIO
  radio.setRetries(15, 15);               // RETRY EVERY 350 milliseconds FOR 15 TIMES
  radio.openWritingPipe(pipes[0]);        // CREATE PIPE TO RECEIVER ADDRESS
  radio.stopListening();                  // ENTER WRITING MODE

  pinMode(buzzer, OUTPUT); // Set buzzer - pin 9 as an output

}


void loop() {

  if (kpd.getKeys())   // IF KEY AVAILABLE
  {
    for (int i = 0; i < LIST_MAX; i++) // Scan the whole key list.
    {
      if ( kpd.key[i].stateChanged )   // Only find keys that have changed state.
      {
        switch (kpd.key[i].kstate)
        { // Report active key state : IDLE, PRESSED, HOLD, or RELEASED

          case PRESSED:
            if (kpd.key[i].kchar == '*')
              tone(buzzer, 2000);                 // SOUND BUZZER ....TURN OFF WHEN KWY RELEASED
            else if (kpd.key[i].kchar == '#')
              tone(buzzer, 1500);                 // SOUND BUZZER ....TURN OFF WHEN KWY RELEASED
            else if (kpd.key[i].kchar >= '0' && kpd.key[i].kchar <= '9')
              tone(buzzer, 600);                 // SOUND BUZZER ....TURN OFF WHEN KWY RELEASED
            else if (kpd.key[i].kchar >= 'A' && kpd.key[i].kchar <= 'D')
              tone(buzzer, 200);                 // SOUND BUZZER ....TURN OFF WHEN KWY RELEASED

            break;

          case RELEASED:
            noTone(buzzer);                    // SOUND OFF BUZZER
            if (kpd.key[i].kchar == '*')
            {
              for (int i = 0; i < 6; i++)
                bus_arr[i] = '\0';
              k = 0;
              send_flag = 0;
            }
            else if (kpd.key[i].kchar == '#')
            {
              if (send_flag == 1)
              {
                bus_arr[k] = '\0';
                send_flag = 0;
                k = 0;
                Serial.println(bus_arr);
                radio.write(&bus_arr, sizeof(bus_arr)); //SEND USING NRF TO RECEIVER
                delay(500);                             // Wait 0.5 second
              }
            }
            else
            {
              if (k < 5)
              {
                bus_arr[k++] = kpd.key[i].kchar;
                send_flag = 1;
              }
            }
            break;
        }
      }
    }
  }
}



